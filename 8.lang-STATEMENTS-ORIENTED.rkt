#lang eopl
;lang-STATEMENTS-ORIENTED


;concrete & abstract syntax tree & scanner/parser
;=====================================================================

;---concrete syntax tree:

;Program :: = Statement

;Statement :: = Identifer = Expression
;             | print Expression
;             | { {Statement}*(;) }
;             | if Expression Statement Statement
;             | while Expression Statement
;             | var {Identifer}*(,) ; Statement

;Expression :: = Number
;              | -(Expression, Expression)
;              | zero? (Expression)
;              | if Expression then Expression else Expression
;              | Identifer
;              | let Identifer = Expression in Expression
;              | proc (Idenfifer) Expression
;              | (Expression Expression)
;              | begin Expression {; Expression}* end
;              | set Identifer = Expression


;---scanner&grammar spec
(define scanner-spec
  '((whitespace (whitespace) skip)
    (comment ("%" (arbno (not #\newline))) skip)
    (identifer (letter (arbno (or letter digit "-" "_" "?"))) symbol)
    (number (digit (arbno digit)) number)
    (number ("-" digit (arbno digit)) number)))
;   (class (regexp) action)
; string (keyword) from grammar-spec (eg: "-","if","let","=",...) has a higher priority than that in scanner-spec.


(define grammar-spec
 '((program (statement) a-program)
   (statement (identifer "=" expression) assign-stat)
   (statement ("print" expression) print-stat)
   (statement ("{" statement (arbno ";" statement) "}") block-stat)
   (statement ("if" expression statement statement) if-stat)
   (statement ("while" expression statement) while-stat)
   (statement ("var" identifer (arbno "," identifer) ";" statement) var-stat)
   (expression (number) const-exp)
   (expression ("-" "(" expression "," expression ")") diff-exp)
   (expression ("zero?" "(" expression ")") zero?-exp)
   (expression ("if" expression "then" expression "else" expression) if-exp)
   (expression (identifer) var-exp)
   (expression ("let" identifer "=" expression "in" expression) let-exp)
   (expression ("proc" "(" identifer ")" expression) proc-exp)
   (expression ("(" expression expression ")") call-exp)
   (expression ("begin" expression (arbno ";" expression) "end") begin-exp)
   (expression ("set" identifer "=" expression) assign-exp)))
;  (algebric-data-type (reg-exp of concrete-syntax with class/string/ADT) constructor)
; class/string -> terminator
; ADT -> nonterminator


;---abstract syntax tree

; use auto-generated datatype for program and expression
;(sllgen:make-define-datatypes scanner-spec grammar-spec)

;see what is generated by calling (show-the-datatypes), the result is following:
;(define show-the-datatypes
;  (lambda () (sllgen:list-define-datatypes scanner-spec grammar-spec)))


;the following datatype can be auto-generated as mentioned above.
(define-datatype program program?
   (a-program (a-program1 statement?)))

(define-datatype statement statement?
   (assign-stat (assign-stat2 symbol?) (assign-stat3 expression?))
   (print-stat (print-stat4 expression?))
   (block-stat (block-stat5 statement?) (block-stat6 (list-of statement?)))
   (if-stat (if-stat7 expression?) (if-stat8 statement?) (if-stat9 statement?))
   (while-stat (while-stat10 expression?) (while-stat11 statement?))
   (var-stat (var-stat12 symbol?) (var-stat13 (list-of symbol?)) (var-stat14 statement?)))

(define-datatype expression expression?
   (const-exp (const-exp15 number?))
   (diff-exp (diff-exp16 expression?) (diff-exp17 expression?))
   (zero?-exp (zero?-exp18 expression?))
   (if-exp (if-exp19 expression?) (if-exp20 expression?) (if-exp21 expression?))
   (var-exp (var-exp22 symbol?))
   (let-exp (let-exp23 symbol?) (let-exp24 expression?) (let-exp25 expression?))
   (proc-exp (proc-exp26 symbol?) (proc-exp27 expression?))
   (call-exp (call-exp28 expression?) (call-exp29 expression?))
   (begin-exp (begin-exp30 expression?) (begin-exp31 (list-of expression?)))
   (assign-exp (assign-exp32 symbol?) (assign-exp33 expression?)))



; auto-generated scanner & parser:

; scan&parse : string -> program
(define scan&parse
  (sllgen:make-string-parser scanner-spec grammar-spec))


; expressed value <-> scheme value
;=====================================================================

; datatype proc is not necessary
(define-datatype proc proc?
  (procedure (var symbol?)
             (body expression?)
             (en env?)))

;------ expval
(define-datatype expval expval?
  (num-val (num number?))
  (bool-val (bool boolean?))
  (proc-val (proc proc?)))


; extractor : expval -> number
(define expval->num
  (lambda (val)
    (cases expval val
      (num-val (num) num)
      (else (eopl:error "expval extract error in expval->num: ~s" val)))))

; extractor : expval -> bool
(define expval->bool
  (lambda (val)
    (cases expval val
      (bool-val (bool) bool)
      (else (eopl:error "expval extract error in expval->bool: ~s" val)))))

; extractor : expval -> proc
(define expval->proc
  (lambda (val)
    (cases expval val
      (proc-val (proc) proc)
      (else (eopl:error "expval extract error in expval->bool: ~s" val)))))


;------ denval
; ref type = number => address in store
(define ref?
  (lambda (ref)
    (number? ref)))


; environment & store
;=====================================================================
;env   : var --- ref
;store :         ref--- expval


;---environment---   

(define-datatype env env?
  (empty-env)
  (extend-env (var symbol?)
              (val ref?) ; !!! in this lang env only stores reference, its value is stored in stores.
              (e env?)))

; init-env
(define init-env
  (extend-env
   'ia345 2
   (extend-env
    'va345 1
    (extend-env
     'xa345 0
     (empty-env)))))


; apply-env : symbol * env -> ref
(define apply-env
  (lambda (var en)
    (cases env en
      (empty-env () (eopl:error "Unbound variable : ~s" var))
      (extend-env (v val e)
                  (if (equal? v var)
                      val
                      (apply-env var e))))))



;---store---
;unlike env, store is global
(define the-store 'uninitialized)

;---interface-for-store

;empty-store : () -> '()
(define empty-store
  (lambda () '()))

(define get-store
  (lambda () the-store))

;---
(define init-store!
  (lambda ()
    (set! the-store (list (num-val 1) (num-val 5) (num-val 10)))))

; eval
;=====================================================================

; auxiliary function for value-of

; apply-procdure : proc * expval -> expval
(define apply-procedure
  (lambda (proc0 val)
    (cases proc proc0
      (procedure (var body en) (value-of body (extend-env var (apply-newref val) en))))))

; apply-newref : expval -> ref
(define apply-newref
  (lambda (val)
    (let ([ref (length the-store)])
      (set! the-store (append the-store (list val)))
      ref)))

; apply-deref : ref -> expval
(define apply-deref
  (lambda (ref)
    (list-ref the-store ref)))

; apply-setref : ref * expval -> unspecified
(define apply-setref!
  (lambda (ref val)
    (define in-setref!
      (lambda (store ref)
        (cond ((null? store) (eopl:error "uninitilized store"))
              ((= 0 ref) (cons val (cdr store)))
              (else (cons (car store)
                          (in-setref! (cdr store) (- ref 1)))))))
    (set! the-store (in-setref! the-store ref))))




; value-of : Exp * Env -> ExpVal
(define value-of
  (lambda (exp env)
    (cases expression exp
      (const-exp (num) (num-val num))
      (var-exp (var) (apply-deref (apply-env var env)))
      (diff-exp (exp1 exp2) (num-val
                             (- (expval->num (value-of exp1 env))
                                (expval->num (value-of exp2 env)))))
      (zero?-exp (exp)
                 (let ((val (expval->num (value-of exp env))))
                   (if (= val 0)
                       (bool-val #t)
                       (bool-val #f))))
      (if-exp (exp1 exp2 exp3)
              (let ((condi (expval->bool (value-of exp1 env))))
                (if condi
                    (value-of exp2 env)
                    (value-of exp3 env))))
      (let-exp (var exp body) (value-of body
                                        (extend-env var
                                                    (apply-newref (value-of exp env))
                                                    env)))
      (proc-exp (param body) (proc-val (procedure param body env)))
      (call-exp (procid exp)
                (let ((proci (expval->proc (value-of procid env)))
                      (param (value-of exp env)))
                  (apply-procedure proci param)))
      (begin-exp (exp exps)
                 (define inner
                   (lambda (expr exprs)
                     (let ((v1 (value-of expr env)))
                         (if (null? exprs)
                         v1
                         (inner (car exprs) (cdr exprs))))))
                 (inner exp exps))
      (assign-exp (var exp) (apply-setref! (apply-env var env) (value-of exp env))))))


; result-of : stat * env -> ()
(define result-of
  (lambda (stat env)
    (cases statement stat
      (assign-stat (var exp) (apply-setref! (apply-env var env) (value-of exp env)))
      (print-stat (exp) (display (value-of exp env)))
      (block-stat (stat stats) (begin (define inner
                                        (lambda (stats)
                                          (if (null? stats)
                                              '()
                                              (begin (result-of (car stats) env) (inner (cdr stats))))))
                                      (result-of stat env)
                                      (inner stats)))
      (if-stat (exp stat1 stat2) (let ([condi (expval->bool (value-of exp env))])
                                   (if (eq? condi #t)
                                       (result-of stat1 env)
                                       (result-of stat2 env))))
      (while-stat (exp stat)
                  (define innerwhile
                    (lambda (exp stat)
                      (let ([condi (expval->bool (value-of exp env))])
                        (if (eq? condi #t)
                            (begin (result-of stat env)
                                   (innerwhile exp stat))
                            '()))))
                  (innerwhile exp stat))
      (var-stat (var vars stat)
                (define vartoenv
                  (lambda (vars env)
                    (if (null? vars)
                        env
                        (vartoenv (cdr vars) (extend-env (car vars) (apply-newref 'uninit-value) env)))))
                (result-of stat (vartoenv (cons var vars) env))))))

; value-of-program : Program -> Expval
(define value-of-program
  (lambda (pgm)
    (init-store!)
    (cases program pgm
      (a-program (stat) (result-of stat init-env)))))


; run : String -> Expval
(define run
  (lambda (str)
    (value-of-program (scan&parse str))))



; test
;=====================================================================

;> (run "{print 123; print 456}")
;  #(struct:num-val 123)#(struct:num-val 456)()

;> (run "var x,y; {x = 3; y = 4; print -(x,y)}")
;  #(struct:num-val -1)()

;> (run "var x; {x = 5; if zero?(x) print 111 print 222}")
;  #(struct:num-val 222)()

;> (run "var x,y ; {x = 0 ; y = 5 ; while zero?(x) {x = -(x,-1); print y}}")
;  #(struct:num-val 5)()

